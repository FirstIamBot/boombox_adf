set(SRCS
    "src/esp_wifi_manager.c"
    "src/esp_wifi_manager_network.c"
    "src/esp_wifi_manager_ap.c"
    "src/esp_wifi_manager_bus.c"
    "src/esp_wifi_manager_nvs.c"
    "src/esp_wifi_manager_http.c"
    "src/esp_wifi_manager_dns.c"
    "src/esp_wifi_manager_mdns.c"
    "src/esp_wifi_manager_boombox.c"
)

set(PRIV_REQUIRES
    esp_bus
    json
    mbedtls
    commons
)

set(EMBED_FILES)

# Add CLI if enabled
if(CONFIG_WIFI_MGR_ENABLE_CLI)
    list(APPEND SRCS "src/esp_wifi_manager_cli.c")
    list(APPEND PRIV_REQUIRES console)
endif()

# Add Web UI if enabled
if(CONFIG_WIFI_MGR_ENABLE_WEBUI)
    list(APPEND SRCS "src/esp_wifi_manager_webui.c")
    # Embed gzipped frontend files
    list(APPEND EMBED_FILES
        "frontend/dist/index.html"
        "frontend/dist/assets/app.js.gz"
        "frontend/dist/assets/index.css.gz"
    )
endif()

idf_component_register(
    SRCS ${SRCS}
    INCLUDE_DIRS
        "include"
    PRIV_INCLUDE_DIRS
        "src"
    REQUIRES
        esp_wifi
        esp_netif
        nvs_flash
        esp_http_server
        esp_event
        mdns
    PRIV_REQUIRES ${PRIV_REQUIRES}
    EMBED_FILES ${EMBED_FILES}
)

# Add BLE support after component registration
# This ensures Kconfig variables are properly resolved
if(CONFIG_BT_ENABLED AND CONFIG_WIFI_MGR_ENABLE_BLE)
    idf_component_get_property(lib ${COMPONENT_NAME} COMPONENT_LIB)
    target_sources(${lib} PRIVATE "src/esp_wifi_manager_ble.c")
    idf_component_get_property(bt_lib bt COMPONENT_LIB)
    target_link_libraries(${lib} PRIVATE ${bt_lib})
endif()

